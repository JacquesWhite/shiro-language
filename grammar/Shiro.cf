-- Programs ---------------------------------------------------------------------

entrypoints       Program ;

PProgram.         Program ::= [Instr] ;
InstBlock.        Block ::= "{" [Instr] "}" ;
separator Instr "";

-- Instructions -------------------------------------------------------------------

InstIncr.         Instr ::= Ident "++" ";";
InstDecr.         Instr ::= Ident "--" ";";

InstAss.          Instr ::= Ident "=" Expr ";";

InstRetExpr.      InstrRetExpr ::= "return" Expr ";";
InstRetUnit.      InstrRetUnit ::= "return" ";";

InstCond.         Instr ::= CondStmt ;
CondIf.           CondStmt ::= "if" Expr Block ;
CondIfElse.       CondStmt ::= "if" Expr Block "else" Block ;
CondIfElseIf.     CondStmt ::= "if" Expr Block "else" CondStmt ;

InstWhile.        Instr ::= "while" Expr Block ;
InstFor.          Instr ::= "for" Ident "from" Expr "to" Expr Block ;

InstPrint.        Instr ::= "print" Expr ";" ;


InstUnit.         Instr ::= ";" ;
InstVal.          Instr ::= Expr ";" ;



-- Declarations -------------------------------------------------------------------

Decl.             Instr ::= Declr ;

DeclFunVal.       Declr ::= "fun" Ident "(" [Param] ")" ":" TypeVal Block ;
DeclFunUnit.      Declr ::= "fun" Ident "(" [Param] ")" ":" TypeVoid Block ;
DeclFunUnitAuto.  Declr ::= "fun" Ident "(" [Param] ")" Block ;

separator Param "," ;

DeclVal.          Declr ::= "val" [DeclrItem] ";" ;
DeclConst.        Declr ::= "const" [DeclrItem] ";" ;
DeclItemType.     DeclrItem ::= Ident "=" Expr ":" TypeVal ;

separator         DeclrItem "," ;

-- Types ------------------------------------------------------------------------

TypeUnit.         TypeVoid ::= "Unit" ;

TypeInt.          TypeVal ::= "Int" ;
TypeBool.         TypeVal ::= "Bool" ;
TypeString.       TypeVal ::= "String" ;

separator TypeVal "," ;

-- Function Types ---------------------------------------------------------------

TypeFun.          TypeVal ::= "(" [ParamType] ")" "->" TypeVal ;
TypeFunUnit.      TypeVal ::= "(" [ParamType] ")" "->" TypeVoid ;

separator ParamType "," ;

ParamDecl.              Param ::= Ident ":" ParamType;

ParamTypeDesc.          ParamType ::= ParamModifier TypeVal ;
ParamModifierConst.     ParamModifier ::= "const" ;
ParamModifierRef.       ParamModifier ::= "ref" ;
ParamModifierNone.      ParamModifier ::= ;
ParamModifierConstRef.  ParamModifier ::= "const" "ref" ;
ParamModifierRefConst.  ParamModifier ::= "ref" "const" ;

-- Expressions ------------------------------------------------------------------

ExprVar.          Expr6 ::= Ident ;
ExprLitInt.       Expr6 ::= Integer ;
ExprLitTrue.      Expr6 ::= "True" ;
ExprLitFalse.     Expr6 ::= "False" ;
ExprLitString.    Expr6 ::= String ;

ExprApp.          Expr6 ::= Ident "(" [Expr] ")" ;

ExprNeg.          Expr5 ::= "-" Expr6 ;
ExprNot.          Expr5 ::= "!" Expr6 ;

ExprMul.          Expr4 ::= Expr4 MulOp Expr5 ;

ExprAdd.          Expr3 ::= Expr3 AddOp Expr4 ;

ExprRel.          Expr2 ::= Expr2 RelOp Expr3 ;

ExprAnd.          Expr1 ::= Expr2 "&&" Expr1 ;

ExprOr.           Expr ::= Expr1 "||" Expr ;

ExprTernary.      Expr ::= Expr1 "?" Expr1 ":" Expr ;

ExprLambdaVal.    Expr ::= "(" [Param] ")" ":" TypeVal "=>" Block ;
ExprLambdaUnit.   Expr ::= "(" [Param] ")" ":" TypeVoid "=>" Block ;

coercions  Expr 6 ;

separator Expr "," ;

-- Operators --------------------------------------------------------------------

OpPlus.           AddOp ::= "+" ;
OpMinus.          AddOp ::= "-" ;
OpTimes.          MulOp ::= "*" ;
OpDiv.            MulOp ::= "/" ;
OpMod.            MulOp ::= "%" ;

OpLt.             RelOp ::= "<" ;
OpLe.             RelOp ::= "<=" ;
OpGt.             RelOp ::= ">" ;
OpGe.             RelOp ::= ">=" ;

OpEq.             RelOp ::= "==" ;
OpNe.             RelOp ::= "!=" ;

-- Comments ---------------------------------------------------------------------

comment         "//" ;
comment         "/*" "*/" ;